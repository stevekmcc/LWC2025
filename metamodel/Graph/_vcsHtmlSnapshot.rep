_vcsHtmlSnapshot(@dir)
_translators()
$single_graph='true' /* We output each graph individually, not building a subgraph hierarchy */
filename @dir 'index.html' encoding 'UTF-8' write
	'<!DOCTYPE html>' newline
	'<html><head>' newline
	'<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />'
	subreport '__stylesheet in HTML' arguments @dir run
	subreport '_styles and scripts for HTML hierarchy' run
	'</head><body>' newline
	'<div class="container">' newline
	' <div class="toc">' newline
	@curProj = ''
	do graphs orderby project unique project
	{	'<h1>' project '</h1>' newline
		'<ul>' newline
		do graphs where project = project;1 orderby id ': ' type
		{	_htmlFile(@dir)
			@href=__(subreport '_id and oid' run '.html')
			'<li><a href="' @href%href '" target="contentPage"><b>' id%xml '</b></a>: ' type%xml '</li>' newline
		}
		'</ul>' newline
	}
	' </div> <!-- toc -->' newline
	' <div class="content">' newline
	'  <iframe name="contentPage" src="' 
		if metatype='Graph' then
			subreport '_id and oid' run '.html'
		else
			'about:blank' /* when called by MetaEdit+ e.g. in versioning */
		endif
		'"></iframe>' newline
	' </div> <!-- content -->' newline
	'</div> <!-- container -->' newline
	'</body></html>'
close